// Author: Francis Sabado and Christian Williams
// Second Life Modeling Project
// Fall 2011
// Artificial Intelligence Project

//GameCondition
//This script acts as the game condition user interface
//for the kinect modeling game
//This object shows the user whether the user won.
//Red means incorrect.
//Greans means correct.(Win)

//This smart object is able to interpret command from the user
//and change attributes automatically

//Defined colors for the object.
vector red = <1.0, 0, 0>;
vector green = < 0, 1.0, 0>;
vector black = < 0, 0,0>;


//--------HTTP Functions---------//

    //User Variables
string URL = "http://turing.csce.uark.edu/~ccw005/SecondLifeServer.php";
string gameTitle = "Kinect Second Life Modeler";
string winMessage = "Congratulations!\nYou win the game!";
key httpkey;
key requestURL;
string objectURL;
integer listen_remove;

// send this objects information to the php server.
phpMethodSelect(string phpMethod)
{
     // Register with the php server by sending the address of this prim to the server with 
     // the name of the prim to act as the primary key.
     
     httpkey = llHTTPRequest(URL,[HTTP_METHOD,"POST",HTTP_MIMETYPE,"application/x-www-form-urlencoded"],"primUrl="+objectURL+"&primName="+llGetObjectName()+"&httpRequest=" + phpMethod);
     
     
//Uncomment if you want predefined names for objects in the database.
//        httpkey = llHTTPRequest(URL,[HTTP_METHOD,"POST",HTTP_MIMETYPE,"application/x-www-form-urlencoded"]
//                          ,"primUrl="+objectURL+"&primName="+ objName +"&httpRequest=" + phpMethod);
}

//Parses the command from the http request for the command interpreter
list parseHTTPCommand(string body)
{
    list command = llParseString2List(body, ["&","="],[]);
    list newCommand = [];
    integer numCommand = llGetListLength(command);
    if(numCommand % 2 != 0 )
    {
        return [];
    }else{
        integer i = 1;
        for( ; i < numCommand; i = 2 + i){
            newCommand = (newCommand =[]) + newCommand +
                     llList2String(command,i);
        }     
    }
        
    integer i;
    integer s = llGetListLength(newCommand);
    //llSay(0,"Saying Command:");
    do
    llOwnerSay(llList2String(newCommand,i));
    while(s>++i);        
                     
    return newCommand;
}


//Preferences
integer editable = 1; // Denote if could be edited; 1 = true , 0 = false

//---------------Change Functions--------------//
//Change Color
//param: float color( r = red, g = green, b = blue)
//integer side( Look at the document for possible side values)
changeColor(float r, float g, float b, integer side)
{
    vector color = < r, g, b>;
    //llSay(0,"Changing color");
    llSetColor(color,side);
}

//--------Communcation and Command Interpreter-------//
//Communication to the object that takes a string
commString(string message){
    list command = llParseString2List(message,[" "],[]); //Parse command
    commList(command);
}
//Communication to the object that takes a list
commList(list command){
    


    integer length = llGetListLength(command);
    if(length == 0){
        return;
    }
    string comm = llList2String(command,0); //command header
    
    //Change editable
    if( comm == "turn"){
        string button = llList2String(command, 1);
        if( button == "on"){
            editable = 1;    
        }else
        if( button == "off"){
            editable = 0;
        }
    }else
    if(editable == 0){
        return;           
    }else
    //Change color
    if( comm == "color"){
        float r = llList2Float(command,1);
        float g = llList2Float(command,2);
        float b = llList2Float(command,3);
            
        if(length != 5){
            changeColor( r, g, b,ALL_SIDES); 
        }else{
            integer sides = (integer) llList2String(command,4);
            changeColor( r, g, b, sides);
        }     
    }
        
}

//------------Debug Code---------//
// Debug incoming requests to see all header information.
debug(key id, string method, string body)
{
    llOwnerSay(method + ": " + body);
    list headers = ["x-script-url","x-path-info","x-query-string",        "x-remote-ip","user-agent"];
    integer i;
    for (i=0;i<5;++i)
    {
        llOwnerSay(llList2String(headers,i) + ": " + llGetHTTPHeader(id,llList2String(headers,i)));
    }
}
            
        
    
//-------------States------------//
default
{
    state_entry()
    {
        llSetText(gameTitle,black,1);
        llSetColor(red,ALL_SIDES);
        
        // get the number of urls for the region.
        integer freeurls = llGetFreeURLs();
        //llSay(0,(string)freeurls);       
        if(freeurls > 0)
        {
             //request a url for this object.
            requestURL = llRequestURL();
        }
        else
        {
             llSay(0, "No urls available!");
        }

        //Waiting for commands
        llListen(0,"",llGetOwner(),"");
        
        phpMethodSelect("removePrim");
    }

    //When regions change, the URLs are invalidated
    //Ask for new URL   
    changed(integer What) {
        //Region restarted
        if (What & CHANGED_REGION_START) {
            //Request new URL
            llRequestURL();
        }
        if(What & CHANGED_COLOR){
            vector currColor = llGetColor(ALL_SIDES);
            if( currColor == green){
                llSetText(winMessage,black,1);
            }else if(currColor == red){
                llSetText(gameTitle,black,1);
            }
        }
    }    
    touch_start(integer total_number)
    {
        //state touched;
    }   
            
    //Command Listener
    //Takes command if editable is 1, ignores if 0 unless turning on
    listen(integer channel, string name, key id, string message)
    {
        //remove prim from database
        if(message == "remove")
       {
            phpMethodSelect("removePrim");
       }else
        //register prim to database
       if(message == "register")      
       {     
            phpMethodSelect("registerPrim");
       }else
        //get url from database
       if(message == "url")
       {
            phpMethodSelect("getPrimUrl");    
       }else
       //get prim list from database
       if(message == "list")
       {
            phpMethodSelect("getPrimList");    
       }
        //Edit the setting if the object accepts messages from user
        if( editable == 1){
            llSay(0,"Interpreting: " + message);
            commString(message); //Command is sent interpreter
        }else{
            if( message == "turn on"){
                llSay(0, "Turning On!");
                editable = 1;
            }     
        }         
    }
            
        
            
                
    // get the responce from the server from any interactions
    http_response(key id, integer status, list meta, string body)
    {
         if(httpkey == id)
         {
             llSay(0,body);
         }           
    }
     
    
    // Parts of the following code method were taken from 
    // http://wiki.secondlife.com/wiki/LSL_http_server/examples
    http_request(key id, string method, string body)
    {
        if ((method == URL_REQUEST_GRANTED) && (id == requestURL) )
        {
            // An URL has been assigned to me.
            llOwnerSay("Obtained URL: " + body);
            objectURL = body;
            // register the prim with the server
            phpMethodSelect("registerPrim");   
            requestURL = NULL_KEY;
        }else 
        if ((method == URL_REQUEST_DENIED) && (id == requestURL)) 
        {
            // I could not obtain a URL
            llOwnerSay("There was a problem, and an URL was not  assigned: " + body);
            requestURL = NULL_KEY;
        }else 
        if (method == "POST") 
        {
            llSay(0,"POST Method Invoked");
            // An incoming message was received.
            //llOwnerSay("Received information from the outside: " +                   body);
            
            llSay(0,body);
            
            //Parse the command such that the object can understand
            list comm = parseHTTPCommand(body);
            llHTTPResponse(id,200,"Thank you for calling. All of our operators are busy.");
            if( llGetListLength(comm) == 0){
                llSay(0,"Invalid argument number");
            }else
            {
                //Sending command to the command interpreter
                commList(comm);    
            }
            

                
        }else 
        if( method == "GET"){
             
            llSay(0,"GET Method Invoked");
            llSay(0,"Body:" + body);
            
            // Process a status update by parsing the query string
            string t = llGetHTTPHeader(id,"x-query-string");
            list l = llParseString2List(t,["?","=","&"],[]);
                
            llSay(0,"Query:" + t);
            
            list headers = [ "x-script-url", "x-path-info", "x-query-string", "x-remote-ip", "user-agent" ];
            integer pos = ~llGetListLength(headers);
             //Output heading for debugging
            while( ++pos )
            {
                string header = llList2String(headers, pos);
                llOwnerSay(header + ": " + llGetHTTPHeader(id, header));
            }
            llOwnerSay( "body: " + body);
             
            llHTTPResponse(id, 200, "Command Succeeded");  
            //Parse the command such that the object can understand
            list comm = parseHTTPCommand(t);
            if( llGetListLength(comm) == 0){
                llSay(0,"Invalid argument number");
            }else
            {
                //Sending command to the command interpreter
                commList(comm);    
            }
               

        }else 
        {
            // An incoming message has come in using a method that has not been anticipated.
                        llSay(0,body);
            llHTTPResponse(id,405,"Unsupported Method");
        }
        
    } 
}

